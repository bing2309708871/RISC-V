### 4.1、分支跳转指令

#### 4.1.1、BEQ、BNE、BLT、BLTU、BGE、BGEQ

#### 4.1.2、defiens.v

```Verilog
// J type inst
`define INST_TYPE_B 7'b1100011
`define INST_BEQ    3'b000
`define INST_BNE    3'b001
`define INST_BLT    3'b100
`define INST_BGE    3'b101
`define INST_BLTU   3'b110
`define INST_BGEU   3'b111

```

#### 4.1.3、id.v

```Verilog
output reg[`MemAddrBus] op1_jump_o,
output reg[`MemAddrBus] op2_jump_o,


`INST_TYPE_B: begin
    case (funct3)
        `INST_BEQ, `INST_BNE, `INST_BLT, `INST_BGE, `INST_BLTU, `INST_BGEU: begin
            reg1_raddr_o = rs1;
            reg2_raddr_o = rs2;
            reg_we_o = `WriteDisable;
            reg_waddr_o = `ZeroReg;
            op1_o = reg1_rdata_i;
            op2_o = reg2_rdata_i;
            op1_jump_o = inst_addr_i;
            op2_jump_o = {{20{inst_i[31]}}, inst_i[7], inst_i[30:25], inst_i[11:8], 1'b0};
        end
        default: begin
            reg1_raddr_o = `ZeroReg;
            reg2_raddr_o = `ZeroReg;
            reg_we_o = `WriteDisable;
            reg_waddr_o = `ZeroReg;
        end
    endcase
end

```

#### 4.1.4、ex.v

```Verilog
assign hold_flag_o = hold_flag;
assign jump_flag_o = jump_flag;
assign jump_addr_o = jump_addr;

`INST_TYPE_B: begin
    case (funct3)
        `INST_BEQ: begin
            jump_flag = op1_eq_op2 & `JumpEnable;
            jump_addr = {32{op1_eq_op2}} & op1_jump_add_op2_jump_res;
        end
        `INST_BNE: begin
            jump_flag = (~op1_eq_op2) & `JumpEnable;
            jump_addr = {32{(~op1_eq_op2)}} & op1_jump_add_op2_jump_res;
        end
        `INST_BLT: begin
            jump_flag = (~op1_ge_op2_signed) & `JumpEnable;
            jump_addr = {32{(~op1_ge_op2_signed)}} & op1_jump_add_op2_jump_res;
        end
        `INST_BGE: begin
            jump_flag = (op1_ge_op2_signed) & `JumpEnable;
            jump_addr = {32{(op1_ge_op2_signed)}} & op1_jump_add_op2_jump_res;
        end
        `INST_BLTU: begin
            jump_flag = (~op1_ge_op2_unsigned) & `JumpEnable;
            jump_addr = {32{(~op1_ge_op2_unsigned)}} & op1_jump_add_op2_jump_res;
        end
        `INST_BGEU: begin
            jump_flag = (op1_ge_op2_unsigned) & `JumpEnable;
            jump_addr = {32{(op1_ge_op2_unsigned)}} & op1_jump_add_op2_jump_res;
        end
        default: begin
            jump_flag = `JumpDisable;
            jump_addr = `ZeroWord;
            reg_wdata = `ZeroWord;
        end
    endcase
end

```

对于BEQ指令，需要添加如下代码：

```Verilog
input wire[`MemAddrBus] op1_jump_i,
input wire[`MemAddrBus] op2_jump_i,

wire op1_eq_op2;
wire[31:0] op1_jump_add_op2_jump_res;
assign op1_eq_op2 = (op1_i == op2_i);
assign op1_jump_add_op2_jump_res = op1_jump_i + op2_jump_i;

```

#### 4.1.5、ctrl.v

为了实现对取指指针的转跳及流水线的暂停，需要新增ctrl.v文件，其代码如下

```Verilog
`include "defines.v"

// 控制模块
// 发出跳转、暂停流水线信号
module ctrl(

    input wire rst,

    // from ex
    input wire jump_flag_i,
    input wire[`InstAddrBus] jump_addr_i,
    input wire hold_flag_ex_i,

    // from rib
    input wire hold_flag_rib_i,
    output reg[`Hold_Flag_Bus] hold_flag_o,

    // to pc_reg
    output reg jump_flag_o,
    output reg[`InstAddrBus] jump_addr_o
    );

    always @ (*) begin
        jump_addr_o = jump_addr_i;
        jump_flag_o = jump_flag_i;
        // 默认不暂停
        hold_flag_o = `Hold_None;
        // 按优先级处理不同模块的请求
        if (jump_flag_i == `JumpEnable || hold_flag_ex_i == `HoldEnable || hold_flag_clint_i == `HoldEnable) begin
            // 暂停整条流水线
            hold_flag_o = `Hold_Id;
        end else if (hold_flag_rib_i == `HoldEnable) begin
            // 暂停PC，即取指地址不变
            hold_flag_o = `Hold_Pc;
        end else if (jtag_halt_flag_i == `HoldEnable) begin
            // 暂停整条流水线
            hold_flag_o = `Hold_Id;
        end else begin
            hold_flag_o = `Hold_None;
        end
    end

endmodule

```

#### 4.1.6、pc_reg.v

pc_reg.v取指模块新增转跳及流水线暂停标志信号

```Verilog
input wire jump_flag_i,                 // 跳转标志
input wire[`InstAddrBus] jump_addr_i,   // 跳转地址
input wire[`Hold_Flag_Bus] hold_flag_i, // 流水线暂停标志

always @ (posedge clk) begin
    // 复位
    if (rst == `RstEnable || jtag_reset_flag_i == 1'b1) begin
        pc_o <= `CpuResetAddr;
        // 跳转
    end else if (jump_flag_i == `JumpEnable) begin
        pc_o <= jump_addr_i;
        // 暂停
    end else if (hold_flag_i >= `Hold_Pc) begin
        pc_o <= pc_o;
        // 地址加4
    end else begin
        pc_o <= pc_o + 4'h4;
    end
end

```

#### 4.1.7、if_id.v

当需要流水线暂停时，寄存器的值保持不变

```Verilog
input wire[`Hold_Flag_Bus] hold_flag_i, // 流水线暂停标志

wire hold_en = (hold_flag_i >= `Hold_If);

wire[`InstBus] inst;
gen_pipe_dff #(32) inst_ff(clk, rst, hold_en, `INST_NOP, inst_i, inst);
assign inst_o = inst;

```

```

```



### 4.2、CSR指令

控制和状态寄存器，用于配置或记录一些运行的状态。CSR寄存器时处理器核内部的寄存器，有专门的12位地址编码空间。

CSRRW、CSRRS、CSRRC、CSRRWI、CSRRSI、CSRRCI指令。

csrrw rd, csr, rs1

将csr索引的CSR寄存器值读出，写回结果寄存器rd中；将操作数寄存器rs1中的值写入csr索引的CSR寄存器中。

#### 4.2.1、defines.v

```Verilog
// CSR inst
`define INST_CSR    7'b1110011
`define INST_CSRRW  3'b001
`define INST_CSRRS  3'b010
`define INST_CSRRC  3'b011
`define INST_CSRRWI 3'b101
`define INST_CSRRSI 3'b110
`define INST_CSRRCI 3'b111

// CSR reg addr
`define CSR_CYCLE   12'hc00
`define CSR_CYCLEH  12'hc80
`define CSR_MTVEC   12'h305
`define CSR_MCAUSE  12'h342
`define CSR_MEPC    12'h341
`define CSR_MIE     12'h304
`define CSR_MSTATUS 12'h300
`define CSR_MSCRATCH 12'h340
```

#### 4.2.2、id.v

```Verilog
`INST_CSR: begin
    reg_we_o = `WriteDisable;
    reg_waddr_o = `ZeroReg;
    reg1_raddr_o = `ZeroReg;
    reg2_raddr_o = `ZeroReg;
    csr_raddr_o = {20'h0, inst_i[31:20]};
    csr_waddr_o = {20'h0, inst_i[31:20]};
    case (funct3)
        `INST_CSRRW, `INST_CSRRS, `INST_CSRRC: begin
            reg1_raddr_o = rs1;
            reg2_raddr_o = `ZeroReg;
            reg_we_o = `WriteEnable;
            reg_waddr_o = rd;
            csr_we_o = `WriteEnable;
        end
        `INST_CSRRWI, `INST_CSRRSI, `INST_CSRRCI: begin
            reg1_raddr_o = `ZeroReg;
            reg2_raddr_o = `ZeroReg;
            reg_we_o = `WriteEnable;
            reg_waddr_o = rd;
            csr_we_o = `WriteEnable;
        end
        default: begin
            reg_we_o = `WriteDisable;
            reg_waddr_o = `ZeroReg;
            reg1_raddr_o = `ZeroReg;
            reg2_raddr_o = `ZeroReg;
            csr_we_o = `WriteDisable;
        end
    endcase
end
```

#### 4.2.3、ex.v

```Verilog
`INST_CSR: begin
    jump_flag = `JumpDisable;
    hold_flag = `HoldDisable;
    jump_addr = `ZeroWord;
    mem_wdata_o = `ZeroWord;
    mem_raddr_o = `ZeroWord;
    mem_waddr_o = `ZeroWord;
    mem_we = `WriteDisable;
    case (funct3)
        `INST_CSRRW: begin
            csr_wdata_o = reg1_rdata_i;
            reg_wdata = csr_rdata_i;
        end
        `INST_CSRRS: begin
            csr_wdata_o = reg1_rdata_i | csr_rdata_i;
            reg_wdata = csr_rdata_i;
        end
        `INST_CSRRC: begin
            csr_wdata_o = csr_rdata_i & (~reg1_rdata_i);
            reg_wdata = csr_rdata_i;
        end
        `INST_CSRRWI: begin
            csr_wdata_o = {27'h0, uimm};
            reg_wdata = csr_rdata_i;
        end
        `INST_CSRRSI: begin
            csr_wdata_o = {27'h0, uimm} | csr_rdata_i;
            reg_wdata = csr_rdata_i;
        end
        `INST_CSRRCI: begin
            csr_wdata_o = (~{27'h0, uimm}) & csr_rdata_i;
            reg_wdata = csr_rdata_i;
        end
        default: begin
            jump_flag = `JumpDisable;
            hold_flag = `HoldDisable;
            jump_addr = `ZeroWord;
            mem_wdata_o = `ZeroWord;
            mem_raddr_o = `ZeroWord;
            mem_waddr_o = `ZeroWord;
            mem_we = `WriteDisable;
            reg_wdata = `ZeroWord;
        end
    endcase
end
```

#### 4.2.4、csr_reg.v

```Verilog
`include "defines.v"

// CSR寄存器模块
module csr_reg(

    input wire clk,
    input wire rst,

    // form ex
    input wire we_i,                        // ex模块写寄存器标志
    input wire[`MemAddrBus] raddr_i,        // ex模块读寄存器地址
    input wire[`MemAddrBus] waddr_i,        // ex模块写寄存器地址
    input wire[`RegBus] data_i,             // ex模块写寄存器数据

    output wire global_int_en_o,            // 全局中断使能标志

    // to ex
    output reg[`RegBus] data_o              // ex模块读寄存器数据
    );

    reg[`DoubleRegBus] cycle;
    reg[`RegBus] mtvec;
    reg[`RegBus] mcause;
    reg[`RegBus] mepc;
    reg[`RegBus] mie;
    reg[`RegBus] mstatus;
    reg[`RegBus] mscratch;

    assign global_int_en_o = (mstatus[3] == 1'b1)? `True: `False;


    // cycle counter
    // 复位撤销后就一直计数
    always @ (posedge clk) begin
        if (rst == `RstEnable) begin
            cycle <= {`ZeroWord, `ZeroWord};
        end else begin
            cycle <= cycle + 1'b1;
        end
    end

    // write reg
    // 写寄存器操作
    always @ (posedge clk) begin
        if (rst == `RstEnable) begin
            mtvec <= `ZeroWord;
            mcause <= `ZeroWord;
            mepc <= `ZeroWord;
            mie <= `ZeroWord;
            mstatus <= `ZeroWord;
            mscratch <= `ZeroWord;
        end else begin
            // 优先响应ex模块的写操作
            if (we_i == `WriteEnable) begin
                case (waddr_i[11:0])
                    `CSR_MTVEC: begin
                        mtvec <= data_i;
                    end
                    `CSR_MCAUSE: begin
                        mcause <= data_i;
                    end
                    `CSR_MEPC: begin
                        mepc <= data_i;
                    end
                    `CSR_MIE: begin
                        mie <= data_i;
                    end
                    `CSR_MSTATUS: begin
                        mstatus <= data_i;
                    end
                    `CSR_MSCRATCH: begin
                        mscratch <= data_i;
                    end
                    default: begin

                    end
                endcase
            end
        end
    end

    // read reg
    // ex模块读CSR寄存器
    always @ (*) begin
        if ((waddr_i[11:0] == raddr_i[11:0]) && (we_i == `WriteEnable)) begin
            data_o = data_i;
        end else begin
            case (raddr_i[11:0])
                `CSR_CYCLE: begin
                    data_o = cycle[31:0];
                end
                `CSR_CYCLEH: begin
                    data_o = cycle[63:32];
                end
                `CSR_MTVEC: begin
                    data_o = mtvec;
                end
                `CSR_MCAUSE: begin
                    data_o = mcause;
                end
                `CSR_MEPC: begin
                    data_o = mepc;
                end
                `CSR_MIE: begin
                    data_o = mie;
                end
                `CSR_MSTATUS: begin
                    data_o = mstatus;
                end
                `CSR_MSCRATCH: begin
                    data_o = mscratch;
                end
                default: begin
                    data_o = `ZeroWord;
                end
            endcase
        end
    end

endmodule
```



### 4.3、异常和中断

在RISC-V里，异常（exception）和中断（interrupt）统称为陷阱（trap），这里的异常又可以称作同步中断，而中断是指异步中断。说到异常和中断，就不得不提RISC-V的特权级别（Privilege Levels）了，RISC-V架构目前一共定义了3种特权级别，由低到高分别是用户、监督者和机器级别（模式）。其中机器模式是必须要实现的，监督者和用户模式根据不同的软件系统需求来实现。一般来说，如果是简单的嵌入式系统，则只需要实现机器模式，如果是安全系统，则需要实现机器和监督者模式，如果是类Unix系统，则这3种模式都要实现。每一种特权级别都有其对应的指令集扩展和CSR寄存器（Control and Status Registers）。由于tinyriscv处理器只实现了机器模式，因此这里只介绍机器模式相关的内容。

先看一些跟中断和异常相关的比较重要的CSR寄存器。注意，机器模式相关的CSR寄存器都是以m字母开头的。

### mstatus（Machine Status Register）

mstatus[3]：MIE，全局中断使能位，可读可写，该位决定了整个核的中断（异常）是否使能。该位对一些不可屏蔽的中断（NMI）是无效的，比如一些会引起硬件错误的中断（异常）。

### mie（Machine Interrupt Enable Register）

mie[3]：MSIE，软件中断使能位，可读可写。

mie[7]：MTIE，定时器中断使能位，可读可写。

mie[11]：MEIE，外部中断使能位，可读可写。

### mip（Machine Interrupt Pending Register）

mip[3]：MSIP，软件中断pending位，只读。

mip[7]：MTIP，定时器中断pending位，只读。

mip[11]：MEIP，外部中断pending位，只读。

### mtvec（Machine Trap-Vector Base-Address Register）

mtvec[31:2]：中断入口基地址，可读可写，必须4字节对齐。

mtvec[1:0]：中断向量模式，可读可写，当mtvec[1:0]=00时为直接模式，此时所有的异常和中断入口地址都为mtvec[31:2]的值。当mtvec[1:0]=01时为向量模式，所有异常的入口地址为mtvec[31:2]的值，而所有中断的入口地址为mtvec[31:2] + causex4，其中cause为中断号。tinyriscv实现的是直接模式。

### mcause（Machine Cause Register）

mcause[31]：中断位，可读可写，表示当trap发生时，该trap中断还是异常，1表示中断，0表示异常。

mcause[30:0]：中断号，可读可写，表示trap发生时所对应的中断（异常）号。比如定时器中断号为7，外部中断号为11，非法指令异常号为2等等。

在中断入口函数里通过读这个寄存器的值就可以知道当前发生的是哪个中断或异常。

### mepc（Machine Exception Program Counter）

该寄存器保存中断（异常）返回时PC指针的值，即MCU处理完中断（异常）后从该寄存器所指的地址处继续执行。